import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'org.openapi.generator' version "$openApiGenVersion"
}

def genFolder = "$rootDir/Generated"
def rtFolder = "$rootDir/Runtime"
def apiConfigPath = project.properties["apiConfigPath"] ?: "$rootDir/oas/vrem-oas.json"

task clean(type: Delete) {
    delete "$genFolder"
    delete "$rtFolder"
}

task tidy(type: Delete) {
    delete "$genFolder"
}

task generateOpenApi(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = 'csharp'
    inputSpec = apiConfigPath
    packageName = "Ch.Unibas.Dmi.Dbis.Vrem.Client"
    outputDir = "$genFolder"

    skipValidateSpec = true

    configOptions = [
            optionalEmitDefaultValues: "true"
    ]
}

task modex(type: Exec) {
    mustRunAfter generateOpenApi

    workingDir "$genFolder"

    if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine "chmod", "+x", "$genFolder/build.sh"
    }
}

task buildOpenApi(type: Exec) {
    dependsOn generateOpenApi
    mustRunAfter modex

    workingDir "$genFolder"

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine = "$genFolder.bat"
    } else {
        dependsOn "modex"
        commandLine = "$genFolder/build.sh"
    }
}

task deployLibs(type: Copy) {
    dependsOn buildOpenApi

    from file("$genFolder/bin")
    into file("$rtFolder/Libs")

    // Automatically rename everything to prevent Unity from identifying packages as duplicates.
    rename('(.*)\\.([a-zA-Z0-9_-]*)', '$1.Vrem.$2')
}

task deployLink(type: Copy) {
    dependsOn buildOpenApi

    from file("$rootDir/link.xml")
    into file("$rtFolder/Libs")
}

task deployAll {
    dependsOn deployLibs
    dependsOn deployLink
}

task('deploy') {
    dependsOn deployAll
    finalizedBy tidy
}
