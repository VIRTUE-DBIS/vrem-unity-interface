import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'org.openapi.generator' version "$openApiGenVersion"
}

def genFolder = "$rootDir/generated"
def rtFolder = "$rootDir/Runtime"

task clean(type: Delete) {
    delete "$genFolder"
    delete "$rtFolder"
    delete fileTree("$rootDir") { include "**/*.meta" }
}

task generateOpenApi(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = 'csharp'
    inputSpec = "vrem-oas.json"
    packageName = "Ch.Unibas.Dmi.Dbis.Vrem.Client"
    outputDir = "$genFolder"

    skipValidateSpec = true

    configOptions = [
            optionalEmitDefaultValues: "true"
    ]
}

task modex(type: Exec) {
    mustRunAfter generateOpenApi

    workingDir "$genFolder"

    if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine "chmod", "+x", "$genFolder/build.sh"
    }
}

task buildOpenApi(type: Exec) {
    dependsOn generateOpenApi
    mustRunAfter modex

    workingDir "$genFolder"

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine = "$genFolder.bat"
    } else {
        dependsOn "modex"
        commandLine = "$genFolder/build.sh"
    }
}

task purgeLibs(type: Delete) {
    mustRunAfter buildOpenApi

    delete "$genFolder/bin/JsonSubTypes.dll"
    // delete "$genFolder/bin/Newtonsoft.Json.dll"
}

task deployLibs(type: Copy) {
    dependsOn buildOpenApi
    dependsOn purgeLibs

    from file("$genFolder/bin")
    into file("$rtFolder")

    // Automatically rename everything to prevent Unity from identifying packages as duplicates.
    rename('(.*)\\.([a-zA-Z0-9_-]*)', '$1.Vrem.$2')
}

task deployLink(type: Copy) {
    dependsOn buildOpenApi

    from file("link.xml")
    into file("$rtFolder")
}

task deploy {
    dependsOn deployLibs
    dependsOn deployLink
}

task deployAndTidy(type: Delete) {
    dependsOn deploy
    mustRunAfter deploy

    delete "$genFolder"
}
